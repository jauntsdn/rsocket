/*
 * Copyright 2015-2020 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'io.spring.dependency-management' apply false
    id 'com.google.protobuf' apply false
    id 'com.github.sherter.google-java-format' apply false
    id 'me.champeau.gradle.jmh' apply false
    id 'io.morethan.jmhreport' apply false
    id 'com.google.osdetector' apply false
    id 'com.palantir.git-version'
    id 'com.gradle.build-scan'
}

description = 'RSocket: Stream Oriented Messaging Passing with Reactive Stream Semantics.'

apply from: 'gradle/dependency-management.gradle'
apply from: 'gradle/publishing.gradle'

allprojects {
    dependencyLocking {
        lockAllConfigurations()
    }
}

subprojects {
    apply plugin: 'com.github.sherter.google-java-format'

    version = projectVersion(project)

    repositories {
        mavenCentral()
    }

    afterEvaluate {
        if (!name.endsWith("test") && !name.endsWith("bom") && !name.endsWith("idl")) {
            dependencies {
                testImplementation 'org.hdrhistogram:HdrHistogram'
                testImplementation 'io.projectreactor:reactor-test'
                testImplementation 'org.assertj:assertj-core'
                testImplementation 'org.junit.jupiter:junit-jupiter-api'
                testImplementation 'org.junit.jupiter:junit-jupiter-params'
                testImplementation 'org.mockito:mockito-core'
                testImplementation 'org.mockito:mockito-junit-jupiter'

                testRuntimeOnly 'ch.qos.logback:logback-classic'
                testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

                // TODO: Remove after JUnit5 migration
                testCompileOnly 'junit:junit'
                testImplementation 'org.hamcrest:hamcrest-library'
                testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
            }
        }
    }

    plugins.withType(JavaPlugin) {
        compileJava {
            sourceCompatibility = 1.8
            dependsOn 'googleJavaFormat'
        }

        javadoc {
            options.with {
                links jdkJavaDoc()
                links 'https://projectreactor.io/docs/core/release/api/'
                links 'https://netty.io/4.1/api/'
            }
        }

        tasks.named("javadoc").configure {
            onlyIf { System.getenv('SKIP_RELEASE') != "true" }
        }

        test {
            useJUnitPlatform()

            testLogging {
                events "failed"
                exceptionFormat "full"
            }
        }
    }

    plugins.withType(JavaLibraryPlugin) {

        task sourcesJar(type: Jar) {
            classifier 'sources'
            from sourceSets.main.allJava
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier 'javadoc'
            from javadoc.destinationDir
        }
    }

    googleJavaFormat {
        toolVersion = '1.6'
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

def jdkJavaDoc() {
    def version = JavaVersion.current()
    def majorVersion = version.majorVersion
    if (version.isJava11Compatible()) {
        return "https://docs.oracle.com/en/java/javase/$majorVersion/docs/api/"
    } else {
        return "https://docs.oracle.com/javase/$majorVersion/docs/api/"
    }
}

def projectVersion(project) {
    def versionSuffix = ""
    def branchName = versionDetails().branchName
    if (branchName != null) {
        def isRelease = System.getProperty("release")
        if (branchName == "develop") {
            versionSuffix = "-SNAPSHOT"
        } else if (branchName != 'master' && !isRelease) {
            versionSuffix = "-${branchName.replace("/", "-")}-SNAPSHOT"
        }
    }
    return project.version + versionSuffix
}